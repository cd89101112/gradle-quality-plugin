plugins {
    id 'com.gradle.build-scan' version '3.0'
    id 'groovy'
    id 'jacoco'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'net.researchgate.release' version '2.8.1'
    id 'ru.vyarus.quality' version '3.4.0'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'ru.vyarus.java-lib' version '1.1.2'
    id 'ru.vyarus.github-info' version '1.1.0'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id "pl.droidsonroids.jacoco.testkit" version "1.0.5"
    id 'ru.vyarus.mkdocs' version '1.1.0'
}

sourceCompatibility = 1.7

wrapper {
    gradleVersion = '5.6.4'
}

repositories { jcenter(); gradlePluginPortal() }
dependencies {

    compile 'com.github.spotbugs:spotbugs-gradle-plugin:2.0.1'

    testCompile('org.spockframework:spock-core:1.2-groovy-2.5') {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile 'ru.vyarus:gradle-animalsniffer-plugin:1.5.0'
    testCompile 'de.aaschmid:gradle-cpd-plugin:3.0'
}

group = 'ru.vyarus'
description = 'Gradle quality plugin for Java and Groovy'

compileJava.options.encoding = "UTF-8"
tasks.withType(Test) {
    systemProperty "file.encoding", "UTF-8"
}

github {
    user 'xvik'
    license 'MIT'
}

mkdocs.publish {
    docPath = '3.5.0'
    rootRedirect = true
}

pom {
    developers {
        developer {
            id 'xvik'
            name 'Vyacheslav Rusakov'
            email 'vyarus@gmail.com'
        }
    }
}

bintray {
    user = findProperty('bintrayUser')
    key = findProperty('bintrayKey')
    publications = ['maven']
    dryRun = false
    publish = true
    pkg {
        repo = 'xvik'
        name = project.name
        desc = project.description
        labels = ['gradle', 'java', 'groovy', 'quality', 'checkstyle', 'pmd', 'spotbugs', 'findbugs', 'codenarc']
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
                passphrase = findProperty('gpgPassphrase')
            }
            mavenCentralSync {
                sync = true
                user = findProperty('sonatypeUser')
                password = findProperty('sonatypePassword')
            }
        }
    }
}

pluginBundle {
    description = 'Static code analysis for Java and Groovy projects'
    tags = ['java', 'groovy', 'quality', 'checkstyle', 'pmd', 'spotbugs', 'findbugs', 'codenarc']

    plugins {
        qualityPlugin {
            id = 'ru.vyarus.quality'
            displayName = project.description
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
    }
}

afterReleaseBuild {
    dependsOn = [bintrayUpload, publishPlugins]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    testLogging {
        events 'skipped', 'failed', 'standard_error'
        exceptionFormat 'full'
    }
    maxHeapSize = '2g'
    forkEvery = 4
    doLast {
        sleep(1000)
    }
}

dependencyUpdates {
    doFirst {
        project.group = 'foo'
    }
    revision = 'release'
}

jacocoTestReport.reports.xml.enabled = true

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    publishOnFailure()
}
